apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.android.library'

group = 'SchillingCoin-android-wallet.integration-android'
description = 'SchillingCoin Integration for Android'
version = '1.0.0'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.gms:google-services:3.2.1'
    }
}

repositories {
    jcenter()
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
    }

    // Rename the aar correctly
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            output.processManifest.doLast {
                if (output != null && output.name.endsWith('.aar')) {
                    outputFileName = "${project.name}-${variant.baseName}-${version}.aar"
                }
            }
        }
    }

    dependencies {
        provided 'javax.annotation:jsr250-api:1.0'
    }
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
        task.description = "Create jar artifact for ${variant.name}"
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        task.destinationDir = project.file("${project.buildDir}/outputs/jar")
        task.archiveName = "${project.name}-${variant.baseName}-${version}.jar"
        artifacts.add('archives', task);
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

dependencies {
    implementation 'com.madgag.spongycastle:core:1.51.0.0'
    testImplementation 'junit:junit:4.12'
}
